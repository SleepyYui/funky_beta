#
  Copyright (C) 2024 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU Library General Public License, version 2, or
  (at your option) under the terms of the GNU Lesser General Public License,
  version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU Lesser (Library) General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files LGPLv2.txt and LGLPv3.txt or
  http://www.gnu.org/licenses/lgpl-2.0.html
  http://www.gnu.org/licenses/lgpl-3.0.html
  or print to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

#
  Topic: Input-Output Events

  Events are the Funky way to handle multiple input/output streams in a
  parallel way.

  The event system is based on the concept of an event "loop". The event "loop"
  is part of the application program.

  Before entering the event loop, the application program must create an
  input-output object and register all input-output streams that should be
  handled by the event loop.

  To create an input-output object, simply clone the object <std_types::io>.

  The event loop then waits for an event to occur. When an event occurs, the
  event loop calls the event handler function associated with the event.

  The event handler function can then process the event and return to the
  event loop.

  The event loop continues to wait for the next event.

  Possible events can be registered or unregistered at any time.

  To register an event call

  <start_reading_from> - for reading from a stream

  <start_catching_signals> - for catching signals like SIGINT

  <start_injecting_window_change_events> - for catching window change events

  To unregister an event call

  <stop_reading_from> - to stop reading from a stream

  <stop_catching_signals> - to stop catching signals

  <stop_injecting_window_change_events> - to stop injecting window change events

  In the event loop call

  <get_events> - to get one or more events

  Actual input operations are performed by <get_events>. Write operations are
  initiated by the application program and performed by <get_events>.

  See also: @Input-Output, @Input-Output Task Manager

  Example:

    $io std_types::io # create an input-output object
    start_reading_from &io STDIN_FILENO # register reading from STDIN
    write &io STDOUT_FILENO "Please enter some text:@nl;"
      # write a prompt text to STDOUT
    $input_buffer "" # create an initially empty input buffer

    loop: # the event loop
      get_events! &io $events # get one or more events
      for_each events # handle all received events
	: (event)
	  event $type $fd $argument # destructure the event
	  case type # check the event type
	    READ: # event is a read event
	      case fd # check the file descriptor
		STDIN_FILENO: # it was a read event from STDIN
		  append &input_buffer argument
		    # append the received data to the input buffer
		  handle_input !io &input_buffer # call the input handler
		  if
		    io.is_undefined:
		      #
			the input handler returned undefined to indicate
			that the input processing is finished
		      terminate! # terminate the program
		    next # otherwise continue processing the events
		next # ignore read events for other file descriptors
	    READ_ERROR: # event is a read error
	      Error! "Error reading from standard input!"
	    WRITE_PENDING: # event is a write pending event
	      # ignore
	      next! # continue processing the events
	    WRITE_COMPLETED: # event is a write completed event
	      # ignore
	      next! # continue processing the events
	    next # ignore all other events
	next # check for more events

    $handle_input: (buffer)
      loop:
	if
	  buffer .contains. '@nl;':
	    # the input buffer contains at least one complete line
	    $line buffer .before. '@nl;' # extract the line
	    if
	      line == "quit" # the user entered "quit"
	      -> undefined undefined
		#
		  return undefined to indicate that the input processing is
		  finished
	      : # Output the line
		write &io STDOUT_FILENO "
		  You entered: "@(line)"
		behind &buffer '@nl;'
		  # remove the processed line from the input buffer
		next # continue processing the buffer
	  -> io buffer
	    # return the (maybe) modified input-output object and buffer

$std::READ .
  #
    used to tag "read" events

    Topic: Input-Output Events

    See also: CONNECT, CLOSED, READ_ERROR, WRITE_PENDING, WRITE_COMPLETED,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::CLOSED .
  #
    used to tag "closed" events

    Topic: Input-Output Events

    See also: READ, READ_ERROR

$std::READ_ERROR .
  #
    used to tag "read error" events

    Topic: Input-Output Events

    See also: READ, CLOSED, CONNECT, WRITE_PENDING, WRITE_COMPLETED,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::CONNECT .
  #
    used to tag "connected" socket events

    Topic: Input-Output Events

    See also: READ, READ_ERROR, WRITE_PENDING, WRITE_COMPLETED,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::WRITE_PENDING .
  #
    used to tag "write pending" events

    Topic: Input-Output Events

    See also: READ, CONNECT, READ_ERROR, WRITE_COMPLETED,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::WRITE_COMPLETED .
  #
    used to tag "write completed" events

    Topic: Input-Output Events

    See also: READ, CONNECT, READ_ERROR, WRITE_PENDING,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::WRITE_ERROR .
  #
    used to tag "write error" events

    Topic: Input-Output Events

    See also: READ, CONNECT, READ_ERROR, WRITE_PENDING,
	      WRITE_COMPLETED, PROCESS_JOINED, SIGNAL

$std::PROCESS_JOINED .
  #
    used to tag "process joined" events

    Topic: Input-Output Events

    See also: READ, CONNECT, READ_ERROR, WRITE_PENDING,
	      WRITE_COMPLETED, WRITE_ERROR, SIGNAL

$std::SIGNAL .
  #
    used to tag "signal" events

    Topic: Input-Output Events

    See also: READ, CONNECT, READ_ERROR, WRITE_PENDING,
	      WRITE_COMPLETED, WRITE_ERROR, PROCESS_JOINED

$std::initialize ()

$std::get_events ()
  #
    gets one or more events

    Result:

      events # a list of events

    Each event is a tuple with the following components:

    *  type - the event type

    *  fd - the file descriptor associated with the event

    *  argument - an argument associated with the event

    This function needs I/O-access rights.

    For a detailed example see <@Input-Output Events>.

    Topic: Input-Output Events

    See also: READ, READ_ERROR, WRITE_PENDING, WRITE_COMPLETED,
	      WRITE_ERROR, PROCESS_JOINED, SIGNAL

$std::start_reading_from ()
  #
    registers a read request for a file descriptor

    Parameters:
      io # an input-output object
      fd # the file descriptor of the stream

    For a detailed example see <@Input-Output Events>.

    Topic: Input-Output Events

    See also: stop_reading_from, get_events

$std::stop_reading_from ()
  #
    unregisters a read request for a file descriptor

    Parameters:
      io # an input-output object
      fd # the file descriptor of the stream

    For a detailed example see <@Input-Output Events>.

    Topic: Input-Output Events

    See also: start_reading_from, get_events

$std::cancel_write ()
  #
    cancels a write operation

    Parameters:
      io # an input-output object
      fd # the file descriptor of the stream

    For a detailed example see <@Input-Output Events>.

    Topic: Input-Output Events

    See also: write, get_events

$std::join_process ()
  #
    joins a process

    Parameters:
      io # an input-output object
      pid # the process ID

    Topic: Input-Output Events

    See also: get_events, PROCESS_JOINED

    Example:

      create_process! $pid $in_fd $out_fd $err_fd command arguments
      ...
      join_process &io pid
      ...
      loop:
	get_events! &io $events
	  for_each events
	    : (event)
	      event $type $epid $argument
	      case type
		PROCESS_JOINED:
		  if
		    pid == epid:
		      ...

$std::start_injecting_window_change_events ()
  #
    starts receiving window change events

    Parameters:
      io # an input-output object

    Topic: Input-Output Events

    See also: stop_injecting_window_change_events, get_events

$std::stop_injecting_window_change_events ()
  #
    stops receiving window change events

    Parameters:
      io # an input-output object

    Topic: Input-Output Events

    See also: start_injecting_window_change_events, get_events

$std::start_catching_signals ()
  #
    starts catching signals

    Parameters:
      io # an input-output object

    Topic: Input-Output Events

    See also: stop_catching_signals, get_events

$std::stop_catching_signals ()
  #
    stops catching signals

    Parameters:
      io # an input-output object

    Topic: Input-Output Events

    See also: start_catching_signals, get_events

$std::set_timeout ()
  #
    sets a timeout for getting events

    Parameters:
      io # an input-output object
      timeout # a timeout or point in time

    Topic: Input-Output Events

    See also: get_events

$std::enable_logging ()
  #
    enables logging

    Parameters:
      io # an input-output object
      level = 1 # the log level

    level:

    0: no logging (the same as <disable_logging>)
    1: minimum logging
    ...

    Topic: Input-Output Events

    See also: log, get_events

$std::disable_logging ()
  #
    disables logging

    Parameters:
      io # an input-output object

    Sets the log level to 0.

    Topic: Input-Output Events

    See also: log, get_events

$std::log ()
  #
    logs a message

    Parameters:
      io # an input-output object
      level = 1 # the log level
      message # the log message

    Topic: Input-Output Events

    See also: enable_logging, disable_logging, get_events

$std::random_seed_of ()
  #
    sets or gets the random seed associated with an input-output object

    Topic: Input-Output Events

$std::step_no_of ()
  #
    gets the "step number" associated with an input-output object

    The "step number" is incremented after each call to <get_events>.

    Topic: Input-Output Events

$std::current_time_of ()
  #
    gets the current time associated with an input-output object

    Topic: Input-Output Events

$read_descriptor_list_of ()
$read_descriptor_set_of ()
$write_jobs_of ()
$close_jobs_of ()
$watched_pids_of ()
$exit_codes_of ()
$do_inject_window_change_events ()
$do_catch_signals ()
$window_change_event_occurred ()
$log_level_of ()
$exit_code_of ()
$timeout_of () # a <date_and_time> object not a number!

$std_types::io std_types::object
  #
    the prototype for all input-output objects

    Topic: Input-Output Events

$std_types::io.read_descriptor_list_of empty_list
$std_types::io.read_descriptor_set_of empty_hash_set
$std_types::io.write_jobs_of empty_hash_table
$std_types::io.close_jobs_of empty_hash_set
$std_types::io.watched_pids_of empty_hash_set
$std_types::io.exit_codes_of empty_hash_table
$std_types::io.do_inject_window_change_events false
$std_types::io.window_change_event_occurred false
$std_types::io.log_level_of 0
$std_types::io.exit_code_of undefined
$std_types::io.timeout_of undefined
$std_types::io.random_seed_of undefined
$std_types::io.step_no_of 0

$std_types::event tuple(undefined undefined undefined)
  #
    the prototype for all events

    Topic: Input-Output Events

$std_types::io/start_reading_from:
  #
    registers a read reqest for a file descriptor

    Topic: Input-Output Events

    See also: @Input-Output Events, stop_reading_from
  (
    self # the input-output object
    fd # the file descriptor of the stream
  )
  if
    read_descriptor_set_of(self)(fd)
    -> self
    :
      !self.read_descriptor_set_of(fd) true
      push &self.read_descriptor_list_of fd
      -> self

$std_types::io/stop_reading_from:
  #
    unregisters a read request for a file descriptor

    Topic: Input-Output Events

    See also: @Input-Output Events, start_reading_from
  (
    self # the input-output object
    fd # the file descriptor of the stream
  )
  if
    read_descriptor_set_of(self)(fd)
    :
      !self.read_descriptor_set_of(fd) false
      $new_read_descriptor_list empty_list
      for_each read_descriptor_list_of(self)
	: (read_fd)
	  if
	    fd == read_fd
	    next
	    :
	      push &new_read_descriptor_list read_fd
	      next
	:
	  !self.read_descriptor_list_of new_read_descriptor_list
	  -> self
    -> self

$std_types::io/write:
  #
    writes data to a stream

    Topic: Input-Output Events

    See also: @Input-Output Events, cancel_write
  (
    self # the input-output object
    fd # the file descriptor of the stream
    data # the data to write
  )
  $job write_jobs_of(self)(fd)
  !job
    if
      job.is_defined
      -> append(job data)
      -> data
  !self.write_jobs_of(fd) job
  -> self

$std_types::io/cancel_write:
  #
    cancels a write operation

    Topic: Input-Output Events

    See also: @Input-Output Events, write
  (
    self # the input-output object
    fd # the file descriptor of the stream
  )
  $unwritten_text write_jobs_of(self)(fd)
  !self.write_jobs_of(fd) undefined
  -> self unwritten_text

$std_types::io/close:
  #
    closes a stream

    This also stops reading from the stream. If there are pending write
    operations, the close operation is delayed until all write operations
    have been completed.

    Topic: Input-Output Events

    See also: @Input-Output Events, stop_reading_from, cancel_write
  (
    self # the input-output object
    fd # the file descriptor of the stream to close
  )
  stop_reading_from &self fd
  !self.close_jobs_of(fd) true
  -> self

$std_types::io/join_process:
  #
    joins a process

    Topic: Input-Output Events

    See also: @Input-Output Events, get_events
  (
    self # the input-output object
    pid # the process ID
  )
  !self.watched_pids_of(pid) true
  -> self

$std_types::io/start_injecting_window_change_events:
  #
    starts receiving window change events

    Topic: Input-Output Events

    See also: @Input-Output Events, stop_injecting_window_change_events
  (
    self # the input-output object
  )
  -> self(.do_inject_window_change_events true)

$std_types::io/stop_injecting_window_change_events:
  #
    stops receiving window change events

    Topic: Input-Output Events

    See also: @Input-Output Events, start_injecting_window_change_events
  (
    self # the input-output object
  )
  -> self(.do_inject_window_change_events false)

$std_types::io/start_catching_signals:
  #
    starts catching signals

    Topic: Input-Output Events

    See also: @Input-Output Events, stop_catching_signals
  (
    self # the input-output object
  )
  -> self(.do_catch_signals true)

$std_types::io/stop_catching_signals:
  #
    stops catching signals

    Topic: Input-Output Events

    See also: @Input-Output Events, start_catching_signals
  (
    self # the input-output object
  )
  -> self(.do_catch_signals false)

$std_types::io/set_timeout:
  #
    sets the timeout for getting events

    Must be called after the input-output object's current time has been set.

    Topic: Input-Output Events

    See also: @Input-Output Events, get_events
  (
    self # the input-output object
    timeout # a timeout or point in time
  )
  update_if timeout.is_a_number &timeout -> current_time_of(self)+timeout
  $current_timeout timeout_of(self)
  if
    current_timeout.is_undefined || timeout < current_timeout
    -> self(.timeout_of timeout)
    -> self

$std_types::io/enable_logging:
  #
    enables logging

    Topic: Input-Output Events

    See also: @Input-Output Events, disable_logging
  (
    self # the input-output object
    level = 1 # the log level
  )
  -> self(.log_level_of level)

$std_types::io/disable_logging:
  #
    disables logging

    Topic: Input-Output Events

    See also: @Input-Output Events, enable_logging
  (
    self # the input-output object
  )
  -> self(.log_level_of 0)

$std_types::io/log:
  #
    logs a message

    Topic: Input-Output Events

    See also: @Input-Output Events, enable_logging, disable_logging
  (
    self # the input-output object
    level = 1 # the log level
    message # the log message
  )
  if
    level <= log_level_of(self):
      update_if_not message.is_a_string &message -> message()
      update_if_not message .has_suffix. '@nl;' &message: push message '@nl;'
      $lines split_into_lines(message)
      $prefix "[@(level)] "
      $buf append(prefix lines(1))
      range &lines 2 -1
      for_each lines
	: (line)
	  append &buf spaces(length_of(prefix)) line
	  next
	:
	  write self STDERR_FILENO buf.to_utf8
    -> self

$std_types::io/exit:
  #
    exits the program

    Topic: Input-Output Events

    See also: @Input-Output Events
  (
    self # the input-output object
    exit_code # the exit code
  )
  ->
    self
      .exit_code_of exit_code
      .read_descriptor_list_of empty_list
      .read_descriptor_set_of empty_hash_set

$std_types::io/is_empty:
  #
    checks if the input-output object contains any event registrations

    Topic: Input-Output Events

    See also: @Input-Output Events
  (
    self # the input-output object
  )
  ->
    &&
      read_descriptor_list_of(self).is_empty
      write_jobs_of(self).is_empty
      close_jobs_of(self).is_empty
      watched_pids_of(self).is_empty

$std::log_unhandled_event: (io event)
  event $type $fd $_argument
  if
    &&
      ||
	type == WRITE_ERROR
	type == WRITE_PENDING
	type == WRITE_COMPLETED
      fd == STDERR_FILENO
    -> io # prevent recursive logging
    :
      $log_level
	case
	  type
	  WRITE_PENDING,WRITE_COMPLETED -> 4
	  -> 2
      log io log_level -> "
	unhandled event @(serialize(type).trim_right) - @(serialize(fd))@

$std_types::io/initialize:
  #
    initializes the input-output object

    This function needs I/O-access rights.

    Topic: Input-Output Events

    See also: @Input-Output Events
  (
    self # the input-output object
  )
  current_time! $t
  -> self(.current_time_of t)

$std_types::io/get_events:
  #
    gets one or more events

    For an example see <@Input-Output Events>.

    Topic: Input-Output Events

    See also: @Input-Output Events, read, read_error, write_pending,
  (
    self # the input-output object
  )
  #
    <pselect> does not check for interrupts if a file descriptor is available
    without the need to wait

  inc &self.step_no_of

  attempt_close_operations! &self
    # perform all close jobs without pending writes
  attempt_exit! self
    # exit if nothing to do and exit code is defined

  $want_to_write empty_list

  $timeout timeout_of(self)
  if
    timeout.is_defined:
      !self.timeout_of undefined
      current_time! $t
      !timeout max(timeout-t 0)
      if
	&&
	  timeout > 0
	  any_of
	    exit_codes_of(self): (pid _exit_code)
	      -> watched_pids_of(self)(pid)
	:
	  !timeout 0
	  handle_write_jobs!
	handle_write_jobs
    handle_write_jobs

  $handle_write_jobs:
    for_each write_jobs_of(self)
      : (fd _text)
	push &want_to_write fd
	next!
      wait_for_events

    $wait_for_events:
      pselect!
	$signals
	$can_read read_descriptor_list_of(self)
	$can_write want_to_write
	$_has_out_of_band_data empty_list
	timeout
      current_time! !self.current_time_of
      $events empty_list
      $injected_into_stdin false
      $write_jobs write_jobs_of(self)
      for_each signals
	: (signal)
	  case signal
	    CHILD_CHANGED_STATE:
	      wait! $pid $status
	      exitstatus &status
	      if
		watched_pids_of(self)(pid):
		  !self.watched_pids_of(pid) false
		  push &events new(std_types::event PROCESS_JOINED pid status)
		  next!
		:
		  !self.exit_codes_of(pid) status
		  next!
	    WINDOW_CHANGED_SIZE:
	      !self.window_change_event_occurred true
	      next!
	    SIGUSR1, SIGUSR2:
	      if
		self.do_catch_signals:
		  push &events new(std_types::event SIGNAL undefined signal)
		  next!
		next
	handle_write_requests

      $handle_write_requests:
	if
	  &&
	    self.window_change_event_occurred
	    self.do_inject_window_change_events
	    read_descriptor_set_of(self)(STDIN_FILENO)
	  :
	    get_terminal_size! STDIN_FILENO $width $height
	    !self.window_change_event_occurred false
	    $buf "@esc;[8;@(height);@(width)t"
	    push &events new(std_types::event READ STDIN_FILENO buf)
	    !injected_into_stdin true
	    check_file_descriptors!
	  :
	    check_file_descriptors!

      $check_file_descriptors:
	for_each can_write
	  : (fd)
	    $job write_jobs(fd)
	    write! $bytes_written fd job
	    if
	      bytes_written.is_an_error:
		!self.write_jobs_of(fd) undefined
		push &events
		  new
		    std_types::event WRITE_ERROR
		    fd
		    tuple(bytes_written job)
		next!
	      :
		if
		  bytes_written < length_of(job):
		    range &job bytes_written+1 -1
		    !self.write_jobs_of(fd) job # remaining text to write
		    push &events new(std_types::event WRITE_PENDING fd job)
		    next!
		  :
		    !self.write_jobs_of(fd) undefined
		    push &events
		      new(std_types::event WRITE_COMPLETED fd undefined)
		    next!
	  attempt_reads

      $attempt_reads:
	for_each can_read
	  : (fd)
	    if_not fd != STDIN_FILENO || not(injected_into_stdin) next:
	      fstat! fd $info
	      if
		type_of(info) == SOCKET:
		  is_listening! $does_listen fd
		  if
		    does_listen:
		      accept! $conn fd
		      if
			conn.is_an_error:
			  push &events
			    new(std_types::event READ_ERROR fd conn)
			  next!
			:
			  push &events
			    new(std_types::event CONNECT fd conn)
			  next!
		    do_read
		do_read

	      $do_read:
		read! $buf fd 0x10'0000
		if
		  buf.is_an_error:
		    push &events new(std_types::event READ_ERROR fd buf)
		    next!
		  :
		    if
		      buf.is_empty:
			stop_reading_from &self fd
			close &self fd
			push &events new(std_types::event CLOSED fd undefined)
			next!
		      :
			push &events new(std_types::event READ fd buf)
			next!
	  :
	    attempt_close_operations! &self
	      # perform all close jobs without pending writes
	    attempt_exit! self
	      # exit if nothing to do and exit code is defined
	    -> self events

$attempt_close_operations: (io)
  for_each close_jobs_of(io)
    : (fd)
      if
	write_jobs_of(io)(fd).is_undefined:
	  close! fd
	  !io.close_jobs_of(fd) false
	  next!
	next
    -> io

$attempt_exit: (io)
  on exit_code_of(io).is_defined && io.is_empty:
    exit! exit_code_of(io)
